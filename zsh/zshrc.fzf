# all but height apply to vim
export FZF_DEFAULT_OPTS='--height=15 --exact --multi --info=inline --layout=reverse --bind tab:down,shift-tab:up,del:toggle,left-click:select,right-click:deselect'

# https://github.com/junegunn/fzf/pull/2885
# dosdevices and SteamLinuxRuntime_soldier (proton) link back to fs roots
export FZF_PATH_PRUNE="\
	\( -fstype 'sysfs' -o -fstype 'devfs' -o -fstype 'devtmpfs' -o -fstype 'proc' \
		-o  -path '*/.git' -o -path '*/target' -o -path '*/node_modules' -o -path '*/__pycache__' \
		-o -path '*dosdevices' -o -path '*SteamLinuxRuntime_soldier' \) -prune \
	-a -not -name '.git' -a -not -name 'target' -a -not -name 'node_modules' -a -not -name '__pycache__' -print"

export FZF_DEFAULT_COMMAND="set -o pipefail; command \
	find -L . -mindepth 1 ${FZF_PATH_PRUNE} \
	-o -type f -print -o -type l -print \
	2> /dev/null | cut -b3-"

export FZF_ALT_C_COMMAND="command \
	find -L . -mindepth 1 -maxdepth 4 ${FZF_PATH_PRUNE} \
	-o -type d -print \
	2> /dev/null | cut -b3-"

export FZF_CTRL_T_COMMAND="command \
	find -L . -mindepth 1 ${FZF_PATH_PRUNE} \
	-o -type f -print -o -type d -print -o -type l -print \
	2> /dev/null | cut -b3-"

_fzf_compgen_path() {
	echo "$1"
	eval "\
		find -L \"$1\" ${FZF_PATH_PRUNE} \
		-o \( -type f -o -type l \) \
		-a -not -path \"$1\" -print | sed 's@^\./@@'"
}

_fzf_compgen_dir() {
	eval "\
		find -L \"$1\" -maxdepth 4 ${FZF_PATH_PRUNE} \
		-o -type d \
		-a -not -path \"$1\" -print \
		2> /dev/null | sed 's@^\./@@'"
}

source /usr/share/fzf/completion.zsh
source /usr/share/fzf/key-bindings.zsh

# overriding zsh tab complete is a bad idea
bindkey '^I' expand-or-complete

# explicitly invoke completion widgets without a trigger
export FZF_COMPLETION_TRIGGER=""
bindkey -M emacs '^ ' fzf-completion
bindkey -M vicmd '^ ' fzf-completion
bindkey -M viins '^ ' fzf-completion

