# all but height apply to vim
export FZF_DEFAULT_OPTS='--height=15 --exact --multi --info=inline --layout=reverse --bind tab:down,shift-tab:up,del:toggle,left-click:select,right-click:deselect'

export FZF_MAX_DEPTH="-maxdepth 4"

# https://github.com/junegunn/fzf/pull/2885
# dosdevices and SteamLinuxRuntime_soldier (proton) link back to fs roots
export FZF_PATH_PRUNE="\
	\( -fstype 'sysfs' -o -fstype 'devfs' -o -fstype 'devtmpfs' -o -fstype 'proc' \
		-o  -path '*/.git' -o -path '*/target' -o -path '*/node_modules' -o -path '*/__pycache__' \
		-o -path '*dosdevices' -o -path '*SteamLinuxRuntime_soldier' \) -prune \
	-a -not -name '.git' -a -not -name 'target' -a -not -name 'node_modules' -a -not -name '__pycache__' -print"

export FZF_DEFAULT_COMMAND="set -o pipefail; command \
	find -L . -mindepth 1 ${FZF_PATH_PRUNE} \
	-o -type f -print -o -type l -print \
	2> /dev/null | cut -b3-"

_fzf_compgen_path() {
	echo "$1"
	eval "\
		find -L \"$1\" ${FZF_MAX_DEPTH} ${FZF_PATH_PRUNE} \
		-o \( -type f -o -type l \) \
		-a -not -path \"$1\" -print | sed 's@^\./@@'"
}

_fzf_compgen_dir() {
	eval "\
		find -L \"$1\" ${FZF_MAX_DEPTH} ${FZF_PATH_PRUNE} \
		-o -type d \
		-a -not -path \"$1\" -print \
		2> /dev/null | sed 's@^\./@@'"
}

fzf-completion-deep() {
	local FZF_MAX_DEPTH_PREV="${FZF_MAX_DEPTH}"
	FZF_MAX_DEPTH=""
	fzf-completion
	FZF_MAX_DEPTH="${FZF_MAX_DEPTH_PREV}"
}
zle -N fzf-completion-deep

source /usr/share/fzf/completion.zsh
source /usr/share/fzf/key-bindings.zsh

# explicitly invoke completion widgets without a trigger
export FZF_COMPLETION_TRIGGER=""

bindkey '^I' expand-or-complete
bindkey -M viins '^@^@' fzf-completion
bindkey -M viins '^@^D' fzf-completion-deep

bindkey -M emacs -r '^T'
bindkey -M viins -r '^T'
bindkey -M vicmd -r '^T'

bindkey -M emacs -r '\ec'
bindkey -M vicmd -r '\ec'
bindkey -M viins -r '\ec'

bindkey -M emacs -r '^R'
bindkey -M vicmd -r '^R'

