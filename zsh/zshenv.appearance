#
# base16 bright, bright colour variants created by multiplying LCh by 1.25 in GIMP
#
typeset -A BASE16
BASE16[00]="000000"; BASE16[black]=$BASE16[00]; BASE16[default_background]=$BASE16[00]   # Default Background
BASE16[01]="303030";                            BASE16[lighter_background]=$BASE16[01]   # Lighter Background (Used for status bars)
BASE16[02]="505050";                            BASE16[selection_background]=$BASE16[02] # Selection Background
BASE16[03]="b0b0b0";                            BASE16[comments]=$BASE16[03]             # Comments, Invisibles, Line Highlighting
BASE16[04]="d0d0d0";                            BASE16[dark_foreground]=$BASE16[04]      # Dark Foreground (Used for status bars)
BASE16[05]="e0e0e0"; BASE16[white]=$BASE16[05]; BASE16[default_foreground]=$BASE16[05]   # Default Foreground, Caret, Delimiters, Operators
BASE16[06]="f5f5f5";                            BASE16[light_foreground]=$BASE16[06]     # Light Foreground (Not often used)
BASE16[07]="ffffff";                            BASE16[light_background]=$BASE16[07]     # Light Background (Not often used)
BASE16[08]="fb0120"; BASE16[red]=$BASE16[08]                                             # RED Variables, XML Tags, Markup Link Text, Markup Lists, Diff Deleted
BASE16[09]="fc6d24"; BASE16[orange]=$BASE16[09]                                          # ORANGE Integers, Boolean, Constants, XML Attributes, Markup Link Url
BASE16[0A]="fda331"; BASE16[yellow]=$BASE16[0A]                                          # YELLOW Classes, Markup Bold, Search Text Background
BASE16[0B]="a1c659"; BASE16[green]=$BASE16[0B]                                           # GREEN Strings, Inherited Class, Markup Code, Diff Inserted
BASE16[0C]="76c7b7"; BASE16[cyan]=$BASE16[0C]                                            # CYAN Support, Regular Expressions, Escape Characters, Markup Quotes
BASE16[0D]="6fb3d2"; BASE16[blue]=$BASE16[0D]                                            # BLUE Functions, Methods, Attribute IDs, Headings
BASE16[0E]="d381c3"; BASE16[magenta]=$BASE16[0E]                                         # MAGENTA Keywords, Storage, Selector, Markup Italic, Diff Changed
BASE16[0F]="be643c"; BASE16[brown]=$BASE16[0F]                                           # BROWN Deprecated, Opening/Closing Embedded Language Tags, e.g. <?php ?>
BASE16[bright_black]="b0b0b0"
BASE16[bright_white]="ffffff"
BASE16[bright_red]="ff4b3f"
BASE16[bright_yellow]="ffd765"
BASE16[bright_green]="d6fc8b"
BASE16[bright_cyan]="aafceb"
BASE16[bright_blue]="9fe3ff"
BASE16[bright_magenta]="ffadf1"

# TODO vim use the arrays instead
for k v in "${(@kv)BASE16}"; do
	export BASE16_${(U)k}="#${v}"
done

#
# user then host coloured
#
typeset -A PROMPT_COL_NAMES
typeset -A PROMPT_COL_RGBS

PROMPT_COL_NAMES[default]="white"
PROMPT_COL_NAMES[root]="red"
PROMPT_COL_NAMES[duke]="yellow"
PROMPT_COL_NAMES[emperor]="green"
PROMPT_COL_NAMES[lord]="cyan"
PROMPT_COL_NAMES[gigantor]="blue"
PROMPT_COL_NAMES[tinygod]="magenta"

for k v in "${(@kv)PROMPT_COL_NAMES}"; do
	PROMPT_COL_RGBS[${k}]=${BASE16[${v}]}
done

if [ -n "${PROMPT_COL_NAMES[${USER}]}" ]; then
	export PROMPT_COL_NAME="${PROMPT_COL_NAMES[${USER}]}"
	export PROMPT_COL_RGB=$PROMPT_COL_RGBS[${USER}]
elif [ -n "${PROMPT_COL_NAMES[${HOST}]}" ]; then
	export PROMPT_COL_NAME="${PROMPT_COL_NAMES[${HOST}]}"
	export PROMPT_COL_RGB=$PROMPT_COL_RGBS[${HOST}]
else
	export PROMPT_COL_NAME=$PROMPT_COL_NAMES[default]
	export PROMPT_COL_RGB=$PROMPT_COL_RGBS[default]
fi

# font size hack scale 1 at 2560x1440 for intellij
case "${HOST}" in
	"gigantor")
		;&
	"tinygod")
		FONT_SIZE_PT=13
		;;
	"emperor")
		FONT_SIZE_PT=12
		;;
	*)
		FONT_SIZE_PT=11
		;;
esac
export FONT_SIZE_PT

# customise various bemenu invocations
export BEMENU_OPTS="\
	--monitor focused \
	--prefix "\>" \
	--prompt ':;' \
	--fn 'monospace ${FONT_SIZE_PT}' \
	--list 10 \
	--wrap \
	--sb '#FFFF00' --sf '#FFFFFF' \
	--tb '#${BASE16[green]}'                --tf '#${BASE16[default_background]}' \
	--fb '#${BASE16[lighter_background]}'   --ff '#${BASE16[dark_foreground]}' \
	--nb '#${BASE16[lighter_background]}'   --nf '#${BASE16[dark_foreground]}' \
	--ab '#${BASE16[lighter_background]}'   --af '#${BASE16[dark_foreground]}' \
	--hb '#${BASE16[selection_background]}' --hf '#${BASE16[light_foreground]}'"

# https://github.com/termstandard/colors
case $TERM in
	linux-truecolor  |\
	tmux-truecolor   |\
	tmux-direct      |\
	xterm-truecolor  |\
	xterm-direct*    |\
	alacritty*       )
		export COLORTERM=truecolor
		;;
	vte*)
esac

