# vim zle mode
bindkey -v

# not using any esc-something sequences so drop the timeout from 40 for better editing responsiveness
KEYTIMEOUT=1

# with proper deletes
bindkey -M viins "${terminfo[kdch1]}" delete-char
bindkey -M vicmd "${terminfo[kdch1]}" delete-char

# shift tab for reverse menu only; tab is still set to expand-or-complete
bindkey -M viins "^[[Z" reverse-menu-complete

# search up to cursor
bindkey -M viins "^J" history-beginning-search-forward
bindkey -M viins "^K" history-beginning-search-backward

# incremental search which uses a limited set of regular bindings, overridden by the (empty) isearch map
bindkey -M viins "^F" history-incremental-search-forward
bindkey -M viins "^B" history-incremental-search-backward

# vim cursor style; using the same VTE values as vim terminus plugin for these nonstandard extensions
CUR_BLOCK="\033[2 q"
CUR_UNDERSCORE="\033[4 q"
CUR_BEAM="\033[6 q"
function updatecursor() {
	case ${KEYMAP} in
		(vicmd)
			printf "${CUR_BLOCK}"
			;;
		(*)
			if [[ "${ZLE_STATE}" =~ "overwrite" ]]; then
				printf "${CUR_UNDERSCORE}"
			else
				printf "${CUR_BEAM}"
			fi
			;;
	esac
}
zle -N zle-line-init updatecursor
zle -N zle-keymap-select updatecursor
function resetcursor() {
	printf "${CUR_BLOCK}"
}
zle -N zle-line-finish resetcursor

# clear the current buffer on ^C, which is attached/unattached to INTR during preexec/precmd
function clearbuffer() {
	BUFFER=""
}
zle -N clearbuffer
bindkey -M viins "^C" clearbuffer
bindkey -M vicmd "^C" clearbuffer

