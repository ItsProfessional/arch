setopt -o promptsubst

COL_OFF="%k%f"
COL_NOR="%F{${COL_DWM_GRAY4}}%K{${COL_DWM_CYAN}}"
COL_WAR="%F{black}%K{yellow}"
COL_ERR="%F{black}%K{red}"

PS1="${COL_NOR}:${COL_OFF}\${PR_WAR}\${PR_ERR}${COL_NOR};${COL_OFF} "
PS2="${COL_NOR}${PS2}${COL_OFF} "
PS3="${COL_NOR}${PS3}${COL_OFF} "
PS4="${COL_NOR}${PS4}${COL_OFF} "

function precmd() {

	# set PR_ERR only if the last execution failed; ignore ^C or empty executions
	rc=${?}
	PR_ERR=
	if [ -n "${PRE_EXECD}" -a "${rc}" -ne 0 ]; then
		PR_ERR="${COL_ERR} ${rc} ${COL_OFF}"
	fi
	PRE_EXECD=

	PR_WAR=
	if [ -z "${TMUX}" ]; then
		PR_WAR="${COL_WAR} -tm ${COL_OFF}"
	fi

	# terminal title
	if [ -n "${ALACRITTY_THEME}" ]; then
		print -Pn "${terminfo[tsl]}%~$(__git_ps1) {${ALACRITTY_THEME}}${terminfo[fsl]}"
	else
		print -Pn "${terminfo[tsl]}%~$(__git_ps1)${terminfo[fsl]}"
	fi
}
function preexec() {
	PRE_EXECD="oui"
}

# git PS1
if [ -f /usr/share/git/completion/git-prompt.sh ]; then
	GIT_PS1_SHOWDIRTYSTATE="true"
	GIT_PS1_SHOWSTASHSTATE="true"
	GIT_PS1_SHOWUPSTREAM="auto"
	. /usr/share/git/completion/git-prompt.sh
else
	function __git_ps1() { : }
fi
