setopt -o promptsubst

PS1="\$(pl_ps1)"
PS2="\$(pl_ps2)"
PS3="\$(pl_ps3)"
PS4="\$(pl_ps4)"


function precmd() {

	# set LAST_RC only if the last execution failed; ignore ^C or empty executions
	rc=${?}
	if [ -z "${PRE_EXECD}" ]; then
		LAST_RC=0
	else
		LAST_RC=${rc}
	fi
	PRE_EXECD=

	# terminal title
	if [ -n "${ALACRITTY_THEME}" ]; then
		print -Pn "${terminfo[tsl]}%~$(__git_ps1) {${ALACRITTY_THEME}}${terminfo[fsl]}"
	else
		print -Pn "${terminfo[tsl]}%~$(__git_ps1)${terminfo[fsl]}"
	fi
}

function preexec() {
	PRE_EXECD="oui"
}


# git PS1
if [ -f /usr/share/git/completion/git-prompt.sh ]; then
	GIT_PS1_SHOWDIRTYSTATE="true"
	GIT_PS1_SHOWSTASHSTATE="true"
	GIT_PS1_SHOWUPSTREAM="auto"
	. /usr/share/git/completion/git-prompt.sh
else
	function __git_ps1() { : }
fi


# powerline prompt builder
# https://github.com/ohmyzsh/ohmyzsh/blob/c1b798aff39942b2f23a0a5f2ef206ebc8ce4970/themes/agnoster.zsh-theme
PL_CUR_BG="NONE"
PL_SEP=$'\ue0b0'

pl_prompt_seg() {
	if [ "${PL_CUR_BG}" != "NONE" -a "${1}" != "${PL_CUR_BG}" ]; then
		echo -n " %{%K{${1}}%F{$PL_CUR_BG}%}$PL_SEP%{%F{${2}}%} "
	else
		echo -n "%{%K{${1}}%}%{%}%F{${2}} "
	fi
	PL_CUR_BG="${1}"
	echo -n "${3}"
}

pl_prompt_end() {
	if [ -n ${PL_CUR_BG} ]; then
		echo -n " %{%k%F{${PL_CUR_BG}}%}${PL_SEP}"
	else
		echo -n "%{%k%}"
	fi
	echo -n "%{%f%} "
	PL_CUR_BG=
}

pl_ps1() {
	pl_prompt_seg "${COL_DWM_CYAN}" "${COL_DWM_GRAY4}" ":"
	if [ "${LAST_RC}" -ne 0 ]; then
		pl_prompt_seg "red" "black" "${LAST_RC}"
	fi
	if [ -z "${TMUX}" ]; then
		pl_prompt_seg "yellow" "black" "!tmux"
	fi
	pl_prompt_seg "${COL_DWM_CYAN}" "${COL_DWM_GRAY4}" ";"
	pl_prompt_end
}

pl_ps2() {
	pl_prompt_seg "${COL_DWM_CYAN}" "${COL_DWM_GRAY4}" "%_>"
	pl_prompt_end
}

pl_ps3() {
	pl_prompt_seg "${COL_DWM_CYAN}" "${COL_DWM_GRAY4}" "?#"
	pl_prompt_end
}

pl_ps4() {
	pl_prompt_seg "${COL_DWM_CYAN}" "${COL_DWM_GRAY4}" "+%N:%i>"
	pl_prompt_end
}

