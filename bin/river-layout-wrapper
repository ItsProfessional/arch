#!/bin/zsh

# rivercarro wrapper to add per-tags and smartborders.
# Assumes 10 tags, with 11th being used a scratch.
# Only left/right/middle layouts supported.
# Functional only for single tags.
# Standalone layout generator may be developed in the future.
# State stored in /tmp/rlw.state

# usage:
#   river-layout-wrapper layout <left|right|middle>
#     apply layout setting borders as appropriate
#   river-layout-wrapper tag-focus <tag>
#     set-focused-tags and apply previous layout for that tag
#   river-layout-wrapper tag-move <tag>
#     set-view-tags, set-focused-tags and apply previous layout for that tag

# example river init:
# echo "llllllllll1" > /tmp/rlw.state
# tag=1
# for k in "a" "o" "e" "u" "i" "semicolon" "comma" "period" "p" "y"; do
# 	riverctl map normal $mod1 $k spawn "river-layout-wrapper tag-focus $tag"
# 	riverctl map normal $mod2 $k spawn "river-layout-wrapper tag-move $tag"
# 	riverctl map normal $mod3 $k set-view-tags $tag
# 	tag=$(($tag << 1))
# done
# riverctl map normal $mod1 h spawn 'river-layout-wrapper layout left'
# riverctl map normal $mod1 t spawn 'river-layout-wrapper layout monocle'
# riverctl map normal $mod1 n spawn 'river-layout-wrapper layout right'

# build masks for convenience
typeset -a tag_masks
tag=1
for i in $(seq 1 10); do
	tag_masks[i]=${tag}
	tag=$((${tag} << 1))
done

# current state
state=$(cat /tmp/rlw.state)
print "river-layout-wrapper ${@} '${state}'"

cmd="${1}"
shift
args="${@}"

# exactly 10 layouts then current tag
typeset -A layouts
for i in $(seq 1 10); do
	# sanitize
	layout=${state[${i}]//[^mlr]/l}
	if [ -z "${layout}" ]; then
		layout="l"
	fi
	layouts[${tag_masks[${i}]}]=${layout}
done

cur_tag=${state:10}
if [[ "$cur_tag" != <-> ]]; then
	# sanitize
	cur_tag=${tag_masks[10]}
fi
cur_layout=${layouts[${cur_tag}]}

case "${cmd}" in
	"layout")
		new_tag=${cur_tag}
		new_layout=${args[1]}
		;;
	"tag-focus")
		;&
	"tag-move")
		new_tag=${args}
		new_layout=${layouts[${new_tag}]}
		;;
	*)
		print "${0} bad command ${cmd}"
		return 1
		;;
esac

print " ${cur_tag}->${new_tag} ${cur_layout}->${new_layout}"

# send view to new
if [ "${cmd}" = "tag-move" ]; then
	riverctl set-view-tags ${new_tag}
fi

# change layout
if [ "${cur_layout}" != "${new_layout}" ]; then

	# change layout in scratch tag to prevent unwanted window resizing in current tag
	if [ "${cur_tag}" != "${new_tag}" ]; then
		cur_tag="1024"
		riverctl set-focused-tags ${cur_tag}
		print " ->${cur_tag}"
	fi

	location="left"
	case "${new_layout}" in
		"m")
			location="monocle"
			;;
		"r")
			location="right"
			;;
		*)
			new_layout="l"
			;;
	esac
	riverctl send-layout-cmd rivercarro "main-location ${location}"
	print " ->${location}"

	# smartborders
	if [ "${location}" = "monocle" ]; then
		riverctl border-width 0
	else
		riverctl border-width 1
	fi
fi

# switch tag
if [ "${cur_tag}" != "${new_tag}" ]; then
	riverctl set-focused-tags ${new_tag}
	print " ->${new_tag}"
fi

# new state
state=""
layouts[${new_tag}]=${new_layout}
for i in ${tag_masks}; do
	state+=${layouts[${i}]}
done
state+=${new_tag}
print " '${state}'"
print "${state}" > /tmp/rlw.state

