#!/bin/zsh

banned=(
	# light
	#"gruvboxlight"
	"pencillight"
	#"seagull"
	#"solarizedlight"

	# colours indistinguishable from background
	"tangoish"

	# ugly
	"ubuntu"

	# washed out
	"nova"
)

base="${HOME}/.config/alacritty"

if [ "${1}" = "--theme" -o "${1}" = "-t" ]; then
	theme="${2}"
	shift
	shift
else
	themes=()
	for f in "${base}/themes/"*yml; do
		theme="${$(basename ${f})/.yml/}"
		if [ "${banned[(ie)${theme}]}" -gt "${#banned}" ]; then
			themes+=${theme}
		fi
	done
	theme="${themes[$(( ( $RANDOM % ${#themes} ) + 1 ))]}"
fi

cfg="/tmp/alacritty.${theme}.yml"

cat "${base}/alacritty.yml" "${base}/themes/${theme}.yml" \
	> "${cfg}"

ALACRITTY_THEME="${theme}" \
	/usr/bin/alacritty \
	--config-file="${cfg}" \
	--no-live-config-reload \
	"$@" \
	&

